
// Dark Servant Artifact ----------------------------------------------------

ACTOR ArtiDarkServant : Inventory native
{
	+COUNTITEM
	+FLOATBOB
	Inventory.RespawnTics 4230
	Inventory.DefMaxAmount
	Inventory.PickupFlash "PickupFlash"
	+INVBAR +FANCYPICKUPSOUND
	Inventory.Icon "ARTISUMN"
	Inventory.PickupSound "misc/p_pkup"
	Inventory.PickupMessage "$TXT_ARTISUMMON"
	Tag "$TAG_ARTISUMMON"
	States
	{
	Spawn:
		SUMN A 350
		Loop
	}
}

// Summoning Doll -----------------------------------------------------------

ACTOR SummoningDoll
{
	Speed 20
	+NOBLOCKMAP +DROPOFF +MISSILE
	+NOTELEPORT

	action native A_Summon();

	States
	{
	Spawn:
		SUMN A 4
		Loop
	Death:
		SUMN AA 4
		SUMN A 4 A_Summon
		Stop
	}
}

// Minotaur Smoke -----------------------------------------------------------

ACTOR MinotaurSmoke
{
	+NOBLOCKMAP +NOGRAVITY
	+NOTELEPORT
	RenderStyle Translucent
	Alpha 0.6

	States
	{
	Spawn:
		MNSM ABCDEFGHIJKLMNOPQ 3
		Stop
	}
}
