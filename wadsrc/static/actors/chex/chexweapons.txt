// Same as Doom weapons, but the obituaries are removed.

actor Bootspoon : Fist
{
	game Chex
	obituary "$OB_MPSPOON"
	Tag "$TAG_SPOON"
}

actor SuperBootspork : Chainsaw 2005
{
	game Chex
	obituary "$OB_MPBOOTSPORK"
	Inventory.PickupMessage "$GOTSUPERBOOTSPORK"
	Tag "$TAG_SPORK"
}

actor MiniZorcher : Pistol
{
	game Chex
	obituary "$OP_MPZORCH"
	inventory.pickupmessage "$GOTMINIZORCHER"
	Tag "$TAG_MINIZORCHER"
	states
	{
		Spawn:
			stop
	}
}

actor LargeZorcher : Shotgun 2001
{
	game Chex
	obituary "$OP_MPZORCH"
	inventory.pickupmessage "$GOTLARGEZORCHER"
	Tag "$TAG_LARGEZORCHER"
}

actor SuperLargeZorcher : SuperShotgun 82
{
	game Chex
	obituary "$OB_MPMEGAZORCH"
	inventory.pickupmessage "$GOTSUPERLARGEZORCHER"
	Tag "$TAG_SUPERLARGEZORCHER"
}

actor RapidZorcher : Chaingun 2002
{
	game Chex
	obituary "$OB_MPRAPIDZORCH"
	inventory.pickupmessage "$GOTRAPIDZORCHER"
	Tag "$TAG_RAPIDZORCHER"
}

actor ZorchPropulsor : RocketLauncher 2003
{
	game Chex
	obituary ""
	inventory.pickupmessage "$GOTZORCHPROPULSOR"
	Tag "$TAG_ZORCHPROPULSOR"
	States
	{
	Fire:
		MISG B 8 A_GunFlash
		MISG B 12 A_FireCustomMissile("PropulsorMissile")
		MISG B 0 A_ReFire
		Goto Ready
	}
}

actor PropulsorMissile : Rocket
{
	-ROCKETTRAIL
	-DEHEXPLOSION
	RenderStyle Translucent
	Obituary "$OB_MPPROPULSOR"
	Alpha 0.75
}	

actor PhasingZorcher : PlasmaRifle 2004
{
	game Chex
	obituary ""
	inventory.pickupmessage "$GOTPHASINGZORCHER"
	Tag "$TAG_PHASINGZORCHER"
	states
	{
	Fire:
		PLSG A 0 A_GunFlash
		PLSG A 3 A_FireCustomMissile("PhaseZorchMissile")
		PLSG B 20 A_ReFire
		Goto Ready
	Flash:
		PLSF A 0 A_Jump(128, "Flash2")
		PLSF A 3 Bright A_Light1
		Goto LightDone
	Flash2:
		PLSF B 3 Bright A_Light1
		Goto LightDone
	}
}

actor PhaseZorchMissile : PlasmaBall
{
	RenderStyle Translucent
	Obituary "$OB_MPPHASEZORCH"
	Alpha 0.75
}	

actor LAZDevice : BFG9000 2006
{
	game Chex
	obituary ""
	inventory.pickupmessage "$GOTLAZDEVICE"
	Tag "$TAG_LAZDEVICE"
	states
	{
	Fire:
		BFGG A 20 A_BFGsound
		BFGG B 10 A_GunFlash
		BFGG B 10 A_FireCustomMissile("LAZBall")
		BFGG B 20 A_ReFire
		Goto Ready
	}
}

actor LAZBall : BFGBall
{
	RenderStyle Translucent
	Obituary "$OB_MPLAZ_BOOM"
	Alpha 0.75
}
